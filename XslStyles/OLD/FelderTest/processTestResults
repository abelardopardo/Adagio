#!/bin/csh
#
# Process the results obtained from one FelderSilverman Test Submission
#

# Src dir from where to obtain the result XML file
if ( "$1" != "" ) then
  set srcDir="$1"
else
  echo "ERROR: Need an argument to get results.xml file."
  exit
endif

# File name to store all answers in XML format
set fname="Q-data.xml"

# Dump header for xml file
echo '<?xml version="1.0" encoding="ISO-8859-1"?>' > $fname
echo "<answers>" >> $fname
# Set the number of given answers
set answerNum=`find .. -maxdepth 1 -name 'Q_[0-9][0-9]-[0-9]' | wc -l`
# If no answer has been given, avoid loop and bomb out
if ( "$answerNum" == 0 ) then
  echo "</answers>" >> $fname
else
    # Loop over all the result files
    foreach file (../Q_[0-9][0-9]-[0-9]) 
    echo  -n '  <answer name="'`basename $file`'">' >> $fname
    cat $file >> $fname
    echo "</answer>"  >> $fname
    end
    echo "</answers>">> $fname
endif

# Copy the result.xml from the source directory to here
if ( ! -e $srcDir/result.xml ) then
  echo "ERROR: No result.xml file found in $srcDir"
  exit
endif

cp $srcDir/result.xml .

if ( ! -e $srcDir/grade.xsl ) then
  echo "ERROR: No grade.xsl file found in $srcDir"
  exit
endif

set subname=`dirname $cwd`
set subname=`basename $subname`

xsltproc --xinclude \
  --stringparam profile.lang es  \
  --stringparam rootPrefix ../../ \
  --stringparam felder.processor 'https://asap.it.uc3m.es/servlet/admit?keyCode=FelderFeedback' \
  --stringparam felder.sublabel "$subname" \
  -o ../results.html \
  $srcDir/grade.xsl \
  result.xml

touch bench.xml
